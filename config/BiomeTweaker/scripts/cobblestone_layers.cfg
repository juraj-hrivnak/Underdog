Tweaker.setPlacementStage("PRE_POPULATE")
##########################################################
# For performance reasons every biome must be listed only once!
# They corespond to cave generator's stone layers
##########################################################
# Generic
generic = forBiomes("biomesoplenty:lush_desert", "minecraft:plains", "minecraft:extreme_hills", "minecraft:extreme_hills_with_trees", "minecraft:mutated_savanna_rock", "biomesoplenty:shrubland", "biomesoplenty:xeric_shrubland", "biomesoplenty:alps_foothills", "biomesoplenty:mountain_foothills", "biomesoplenty:redwood_forest_edge", "biomesoplenty:pasture", "minecraft:beaches", "minecraft:mutated_plains", "biomesoplenty:lavender_fields", "biomesoplenty:crag", "biomesoplenty:flower_field", "biomesoplenty:grove", "minecraft:forest_hills", "minecraft:birch_forest_hills", "minecraft:roofed_forest", "minecraft:mutated_extreme_hills", "minecraft:mutated_forest", "minecraft:mutated_birch_forest", "minecraft:mutated_birch_forest_hills", "minecraft:mutated_roofed_forest", "minecraft:mutated_extreme_hills_with_trees", "biomesoplenty:bamboo_forest", "biomesoplenty:cherry_blossom_grove", "biomesoplenty:mountain", "biomesoplenty:orchard")

toMarble_Generic = newBlockReplacement()
toMarble_Generic.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_Generic.set("maxY", 90)
toMarble_Generic.set("minY", 65)

toGranite_Generic = newBlockReplacement()
toGranite_Generic.set("block", "divergentunderground:granite_cobblestone")
toGranite_Generic.set("maxY", 120)
toGranite_Generic.set("minY", 90)

toAndesite_Generic = newBlockReplacement()
toAndesite_Generic.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_Generic.set("maxY", 200)
toAndesite_Generic.set("minY", 120)

    # Composite Rocks
    # maxHeight: 255

generic.registerGenBlockRep("minecraft:cobblestone", toMarble_Generic)
generic.registerGenBlockRep("minecraft:cobblestone", toGranite_Generic)
generic.registerGenBlockRep("minecraft:cobblestone", toAndesite_Generic)

#########################################
# Highlands
highlands = forBiomes( "biomesoplenty:highland", "biomesoplenty:overgrown_cliffs")

toDiorite_Highlands = newBlockReplacement()
toDiorite_Highlands.set("block", "divergentunderground:diorite_cobblestone")
toDiorite_Highlands.set("maxY", 76)
toDiorite_Highlands.set("minY", 60)

toLimestone_Highlands = newBlockReplacement()
toLimestone_Highlands.set("block", "divergentunderground:quark_limestone_cobblestone")
toLimestone_Highlands.set("maxY", 88)
toLimestone_Highlands.set("minY", 76)

    # toSlate
    # maxHeight: 116

    # Composite Rocks
    # maxHeight: 135

toDiorite2_Highlands = newBlockReplacement()
toDiorite2_Highlands.set("block", "divergentunderground:diorite_cobblestone")
toDiorite2_Highlands.set("maxY", 255)
toDiorite2_Highlands.set("minY", 135)

highlands.registerGenBlockRep("minecraft:cobblestone", toDiorite_Highlands)
highlands.registerGenBlockRep("minecraft:cobblestone", toLimestone_Highlands)
highlands.registerGenBlockRep("minecraft:cobblestone", toDiorite2_Highlands)

#########################################
# Icy
icy = forBiomes("minecraft:ice_flats", "minecraft:ice_mountains", "minecraft:cold_beach", "minecraft:mutated_ice_flats", "biomesoplenty:snowy_tundra", "biomesoplenty:glacier", "biomesoplenty:tundra", "biomesoplenty:cold_desert")

    # Composite Rocks
    # maxHeight: 48

    # toHardIce
    # maxHeight: 64

toLimestone_icy = newBlockReplacement()
toLimestone_icy.set("block", "divergentunderground:quark_limestone_cobblestone")
toLimestone_icy.set("maxY", 255)
toLimestone_icy.set("minY", 64)

icy.registerGenBlockRep("minecraft:cobblestone", toLimestone_icy)

#########################################
# Mesa

    # toJasper
    # maxHeight: 204

    # toJasper2
    # maxHeight: 255

#########################################
# Mudstones
mudstones = forBiomes("minecraft:swampland", "minecraft:mutated_swampland", "minecraft:river", "biomesoplenty:bayou", "biomesoplenty:bog", "biomesoplenty:lush_swamp", "biomesoplenty:mangrove", "biomesoplenty:chaparral", "biomesoplenty:wetland", "biomesoplenty:marsh")

    # Composite Rocks
    # maxHeight: 60

toLimestone_mudstones = newBlockReplacement()
toLimestone_mudstones.set("block", "divergentunderground:quark_limestone_cobblestone")
toLimestone_mudstones.set("maxY", 86)
toLimestone_mudstones.set("minY", 60)

toMarble_mudstones = newBlockReplacement()
toMarble_mudstones.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_mudstones.set("maxY", 255)
toMarble_mudstones.set("minY", 86)

mudstones.registerGenBlockRep("minecraft:cobblestone", toLimestone_mudstones)
mudstones.registerGenBlockRep("minecraft:cobblestone", toMarble_mudstones)

#########################################
# Ocean
    # Not used!

#########################################
# Oceanic Landmass
    # Not Used!

#########################################
# Stone Beach
stoneBeach = forBiomes("minecraft:stone_beach")

toMarble_stoneBeach = newBlockReplacement()
toMarble_stoneBeach.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_stoneBeach.set("maxY", 164)
toMarble_stoneBeach.set("minY", 60)

toGranite_stoneBeach = newBlockReplacement()
toGranite_stoneBeach.set("block", "divergentunderground:granite_cobblestone")
toGranite_stoneBeach.set("maxY", 255)
toGranite_stoneBeach.set("minY", 164)

    # state: minecraft:sandstone
    # maxHeight: 250

stoneBeach.registerGenBlockRep("minecraft:cobblestone", toMarble_stoneBeach)
stoneBeach.registerGenBlockRep("minecraft:cobblestone", toGranite_stoneBeach)

#########################################
# Temperate
temperate = forBiomes("biomesoplenty:grassland", "biomesoplenty:temperate_rainforest", "biomesoplenty:redwood_forest", "biomesoplenty:seasonal_forest")

toAndesite_temperate = newBlockReplacement()
toAndesite_temperate.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_temperate.set("maxY", 128)
toAndesite_temperate.set("minY", 60)

    # Composite Rocks
    # maxHeight: 204

toBasalt_temperate = newBlockReplacement()
Basalt = forBlock("projectred-exploration:stone")
Basalt.setProperty("type", "basalt_cobble")
toBasalt_temperate.set("block", Basalt)
toBasalt_temperate.set("maxY", 255)
toBasalt_temperate.set("minY", 128)

temperate.registerGenBlockRep("minecraft:cobblestone", toAndesite_temperate)
temperate.registerGenBlockRep("minecraft:cobblestone", toBasalt_temperate)

#########################################
# Volcanic
volcanic = forBiomes("biomesoplenty:land_of_lakes", "biomesoplenty:shield", "biomesoplenty:tropical_island", "biomesoplenty:volcanic_island")

toGranite_volcanic = newBlockReplacement()
toGranite_volcanic.set("block", "divergentunderground:granite_cobblestone")
toGranite_volcanic.set("maxY", 90)
toGranite_volcanic.set("minY", 64)

toBasalt_volcanic = newBlockReplacement()
basalt = forBlock("projectred-exploration:stone")
Basalt.setProperty("type", "basalt_cobble")
toBasalt_volcanic.set("block", basalt)
toBasalt_volcanic.set("maxY", 120)
toBasalt_volcanic.set("minY", 90)

toAndesite_volcanic = newBlockReplacement()
toAndesite_volcanic.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_volcanic.set("maxY", 255)
toAndesite_volcanic.set("minY", 120)

volcanic.registerGenBlockRep("minecraft:cobblestone", toGranite_volcanic)
volcanic.registerGenBlockRep("minecraft:cobblestone", toBasalt_volcanic)
volcanic.registerGenBlockRep("minecraft:cobblestone", toAndesite_volcanic)

#########################################
# Wasteland
wasteland = forBiomes("minecraft:mutated_mesa", "minecraft:mutated_mesa_rock", "minecraft:mutated_mesa_clear_rock", "biomesoplenty:wasteland", "biomesoplenty:prairie", "biomesoplenty:brushland")

toGranite_wasteland = newBlockReplacement()
toGranite_wasteland.set("block", "divergentunderground:granite_cobblestone")
toGranite_wasteland.set("maxY", 70)
toGranite_wasteland.set("minY", 60)

toBasalt_wasteland = newBlockReplacement()
Basalt = forBlock("projectred-exploration:stone")
Basalt.setProperty("type", "basalt_cobble")
toBasalt_wasteland.set("block", Basalt)
toBasalt_wasteland.set("maxY", 120)
toBasalt_wasteland.set("minY", 70)

    # Dried Sand
    # maxHeight: 255

wasteland.registerGenBlockRep("minecraft:cobblestone", toGranite_wasteland)
wasteland.registerGenBlockRep("minecraft:cobblestone", toBasalt_wasteland)

#########################################
# Jungle
jungle = forBiomes("minecraft:smaller_extreme_hills", "minecraft:jungle", "minecraft:jungle_hills", "minecraft:jungle_edge", "minecraft:mutated_jungle", "minecraft:mutated_jungle_edge")

toAndesite_jungle = newBlockReplacement()
toAndesite_jungle.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_jungle.set("maxY", 64)
toAndesite_jungle.set("minY", 57)

toDiorite_jungle = newBlockReplacement()
toDiorite_jungle.set("block", "divergentunderground:diorite_cobblestone")
toDiorite_jungle.set("maxY", 150)
toDiorite_jungle.set("minY", 64)

    # Composite Rocks
    # maxHeight: 255

jungle.registerGenBlockRep("minecraft:cobblestone", toAndesite_jungle)
jungle.registerGenBlockRep("minecraft:cobblestone", toDiorite_jungle)

#########################################
# Cold
cold = forBiomes("minecraft:taiga", "minecraft:taiga_hills", "minecraft:taiga_cold", "minecraft:taiga_cold_hills", "minecraft:redwood_taiga", "minecraft:redwood_taiga_hills", "minecraft:mutated_taiga", "minecraft:mutated_taiga_cold", "minecraft:mutated_redwood_taiga", "minecraft:mutated_redwood_taiga_hills", "biomesoplenty:alps", "biomesoplenty:coniferous_forest", "biomesoplenty:snowy_coniferous_forest", "biomesoplenty:snowy_forest", "biomesoplenty:dead_forest", "biomesoplenty:steppe", "biomesoplenty:boreal_forest")

toGranite_cold = newBlockReplacement()
toGranite_cold.set("block", "divergentunderground:granite_cobblestone")
toGranite_cold.set("maxY", 128)
toGranite_cold.set("minY", 60)

    # toSlate
    # maxHeight: 255

cold.registerGenBlockRep("minecraft:cobblestone", toGranite_cold)

# The End :D