###########################################################################
# Vanilla biomes
# Each vanilla biome has its own variable
###########################################################################

vanillaOcean                    = forBiomes("minecraft:ocean")
vanillaPlains                   = forBiomes("minecraft:plains")
vanillaDesert                   = forBiomes("minecraft:desert")
vanillaExtremeHills             = forBiomes("minecraft:extreme_hills")
vanillaForest                   = forBiomes("minecraft:forest")
vanillaTaiga                    = forBiomes("minecraft:taiga")
vanillaSwampland                = forBiomes("minecraft:swampland")
# vanillaRiver                    = forBiomes("minecraft:river")
vanillaHell                     = forBiomes("minecraft:hell")
vanillaTheEnd                   = forBiomes("minecraft:sky")
vanillaFrozenOcean              = forBiomes("minecraft:frozen_ocean")
# vanillaFrozenRiver              = forBiomes("minecraft:frozen_river")
vanillaIcePlains                = forBiomes("minecraft:ice_flats")
vanillaIceMountains             = forBiomes("minecraft:ice_mountains")
vanillaMushroomIsland           = forBiomes("minecraft:mushroom_island")
vanillaMushroomIslandShore      = forBiomes("minecraft:mushroom_island_shore")
vanillaBeach                    = forBiomes("minecraft:beaches")
vanillaDesertHills              = forBiomes("minecraft:desert_hills")
vanillaForestHills              = forBiomes("minecraft:forest_hills")
vanillaTaigaHills               = forBiomes("minecraft:taiga_hills")
vanillaExtremeHillsEdge         = forBiomes("minecraft:smaller_extreme_hills")
vanillaJungle                   = forBiomes("minecraft:jungle")
vanillaJungleHills              = forBiomes("minecraft:jungle_hills")
vanillaJungleEdge               = forBiomes("minecraft:jungle_edge")
vanillaDeepOcean                = forBiomes("minecraft:deep_ocean")
vanillaStoneBeach               = forBiomes("minecraft:stone_beach")
vanillaColdBeach                = forBiomes("minecraft:cold_beach")
vanillaBirchForest              = forBiomes("minecraft:birch_forest")
vanillaBirchForestHills         = forBiomes("minecraft:birch_forest_hills")
vanillaRoofedForest             = forBiomes("minecraft:roofed_forest")
vanillaColdTaiga                = forBiomes("minecraft:taiga_cold")
vanillaColdTaigaHills           = forBiomes("minecraft:taiga_cold_hills")
vanillaMegaTaiga                = forBiomes("minecraft:redwood_taiga")
vanillaMegaTaigaHills           = forBiomes("minecraft:redwood_taiga_hills")
vanillaExtremeHillsPlus         = forBiomes("minecraft:extreme_hills_with_trees")
vanillaSavanna                  = forBiomes("minecraft:savanna")
vanillaSavannaPlateau           = forBiomes("minecraft:savanna_rock")
vanillaMesa                     = forBiomes("minecraft:mesa")
vanillaMesaPlateauF             = forBiomes("minecraft:mesa_rock")
vanillaMesaPlateau              = forBiomes("minecraft:mesa_clear_rock")
vanillaTheVoid                  = forBiomes("minecraft:void")
vanillaSunflowerPlains          = forBiomes("minecraft:mutated_plains")
vanillaDesertM                  = forBiomes("minecraft:mutated_desert")
vanillaExtremeHillsM            = forBiomes("minecraft:mutated_extreme_hills")
vanillaFlowerForest             = forBiomes("minecraft:mutated_forest")
vanillaTaigaM                   = forBiomes("minecraft:mutated_taiga")
vanillaSwamplandM               = forBiomes("minecraft:mutated_swampland")
vanillaIcePlainsSpikes          = forBiomes("minecraft:mutated_ice_flats")
vanillaJungleM                  = forBiomes("minecraft:mutated_jungle")
vanillaJungleEdgeM              = forBiomes("minecraft:mutated_jungle_edge")
vanillaBirchForestM             = forBiomes("minecraft:mutated_birch_forest")
vanillaBirchForestHillsM        = forBiomes("minecraft:mutated_birch_forest_hills")
vanillaRoofedForestM            = forBiomes("minecraft:mutated_roofed_forest")
vanillaColdTaigaM               = forBiomes("minecraft:mutated_taiga_cold")
vanillaMegaSpruceTaiga          = forBiomes("minecraft:mutated_redwood_taiga")
vanillaRedwoodTaigaHills        = forBiomes("minecraft:mutated_redwood_taiga_hills")
vanillaExtremeHillsPlusM        = forBiomes("minecraft:mutated_extreme_hills_with_trees")
vanillaSavannaM                 = forBiomes("minecraft:mutated_savanna")
vanillaSavannaPlateauM          = forBiomes("minecraft:mutated_savanna_rock")
vanillaMesaBryce                = forBiomes("minecraft:mutated_mesa")
vanillaMesaPlateauFM            = forBiomes("minecraft:mutated_mesa_rock")
vanillaMesaPlateauM             = forBiomes("minecraft:mutated_mesa_clear_rock")

###########################################################################
# BOP biomes
# Each BOP biome has its own variable
###########################################################################

bopGravelBeach                  = forBiomes("biomesoplenty:gravel_beach")
bopWhiteBeach                   = forBiomes("biomesoplenty:white_beach")
bopOriginBeach                  = forBiomes("biomesoplenty:origin_beach")
bopAlps                         = forBiomes("biomesoplenty:alps")
bopBambooForest                 = forBiomes("biomesoplenty:bamboo_forest")
bopBayou                        = forBiomes("biomesoplenty:bayou")
bopBog                          = forBiomes("biomesoplenty:bog")
bopBorealForest                 = forBiomes("biomesoplenty:boreal_forest")
bopBrushland                    = forBiomes("biomesoplenty:brushland")
bopChaparral                    = forBiomes("biomesoplenty:chaparral")
bopCherryBlossomGrove           = forBiomes("biomesoplenty:cherry_blossom_grove")
bopColdDesert                   = forBiomes("biomesoplenty:cold_desert")
bopConiferousForest             = forBiomes("biomesoplenty:coniferous_forest")
bopCrag                         = forBiomes("biomesoplenty:crag")
bopDeadForest                   = forBiomes("biomesoplenty:dead_forest")
bopDeadSwamp                    = forBiomes("biomesoplenty:dead_swamp")
bopEucalyptusForest             = forBiomes("biomesoplenty:eucalyptus_forest")
bopFen                          = forBiomes("biomesoplenty:fen")
bopFlowerField                  = forBiomes("biomesoplenty:flower_field")
bopGrassland                    = forBiomes("biomesoplenty:grassland")
bopGrove                        = forBiomes("biomesoplenty:grove")
bopHighland                     = forBiomes("biomesoplenty:highland")
bopLandOfLakes                  = forBiomes("biomesoplenty:land_of_lakes")
bopLavenderFields               = forBiomes("biomesoplenty:lavender_fields")
bopLushDesert                   = forBiomes("biomesoplenty:lush_desert")
bopLushSwamp                    = forBiomes("biomesoplenty:lush_swamp")
bopMangrove                     = forBiomes("biomesoplenty:mangrove")
bopMapleWoods                   = forBiomes("biomesoplenty:maple_woods")
bopMarsh                        = forBiomes("biomesoplenty:marsh")
bopMeadow                       = forBiomes("biomesoplenty:meadow")
bopMoor                         = forBiomes("biomesoplenty:moor")
bopMountain                     = forBiomes("biomesoplenty:mountain")
bopMysticGrove                  = forBiomes("biomesoplenty:mystic_grove")
bopOminousWoods                 = forBiomes("biomesoplenty:ominous_woods")
bopOrchard                      = forBiomes("biomesoplenty:orchard")
bopOutback                      = forBiomes("biomesoplenty:outback")
bopOvergrownCliffs              = forBiomes("biomesoplenty:overgrown_cliffs")
bopPrairie                      = forBiomes("biomesoplenty:prairie")
bopQuagmire                     = forBiomes("biomesoplenty:quagmire")
bopRainforest                   = forBiomes("biomesoplenty:rainforest")
bopRedwoodForest                = forBiomes("biomesoplenty:redwood_forest")
bopSacredSprings                = forBiomes("biomesoplenty:sacred_springs")
bopSeasonalForest               = forBiomes("biomesoplenty:seasonal_forest")
bopShield                       = forBiomes("biomesoplenty:shield")
bopShrubland                    = forBiomes("biomesoplenty:shrubland")
bopSnowyConiferousForest        = forBiomes("biomesoplenty:snowy_coniferous_forest")
bopSnowyForest                  = forBiomes("biomesoplenty:snowy_forest")
bopSteppe                       = forBiomes("biomesoplenty:steppe")
bopTemperateRainforest          = forBiomes("biomesoplenty:temperate_rainforest")
bopTropicalRainforest           = forBiomes("biomesoplenty:tropical_rainforest")
bopTundra                       = forBiomes("biomesoplenty:tundra")
bopWasteland                    = forBiomes("biomesoplenty:wasteland")
bopWetland                      = forBiomes("biomesoplenty:wetland")
bopWoodland                     = forBiomes("biomesoplenty:woodland")
bopXericShrubland               = forBiomes("biomesoplenty:xeric_shrubland")
bopAlpsFoothills                = forBiomes("biomesoplenty:alps_foothills")
bopMountainFoothills            = forBiomes("biomesoplenty:mountain_foothills")
bopPasture                      = forBiomes("biomesoplenty:pasture")
bopGlacier                      = forBiomes("biomesoplenty:glacier")
bopOasis                        = forBiomes("biomesoplenty:oasis")
bopCoralReef                    = forBiomes("biomesoplenty:coral_reef")
bopKelpForest                   = forBiomes("biomesoplenty:kelp_forest")
bopOriginIsland                 = forBiomes("biomesoplenty:origin_island")
bopTropicalIsland               = forBiomes("biomesoplenty:tropical_island")
bopVolcanicIsland               = forBiomes("biomesoplenty:volcanic_island")
bopFlowerIsland                 = forBiomes("biomesoplenty:flower_island")
bopCorruptedSands               = forBiomes("biomesoplenty:corrupted_sands")
bopFungiForest                  = forBiomes("biomesoplenty:fungi_forest")
bopPhantasmagoricInferno        = forBiomes("biomesoplenty:phantasmagoric_inferno")
bopUndergarden                  = forBiomes("biomesoplenty:undergarden")
bopVisceralHeap                 = forBiomes("biomesoplenty:visceral_heap")

###########################################################################
# Traverse biomes, disabled for now
# Each Traverse biome has its own variable
###########################################################################

# traverseAridHighland            = forBiomes("traverse:arid_highland")
# traverseAutumnalWoodedHills     = forBiomes("traverse:autumnal_wooded_hills")
# traverseAutumnalWoods           = forBiomes("traverse:autumnal_woods")
# traverseBadlands                = forBiomes("traverse:badlands")
# traverseBirchForestedHills      = forBiomes("traverse:birch_forested_hills")
# traverseCliffs                  = forBiomes("traverse:cliffs")
# traverseDesertShrubland         = forBiomes("traverse:desert_shrubland")
# traverseForestedHills           = forBiomes("traverse:forested_hills")
# traverseGlacierSpikes           = forBiomes("traverse:glacier_spikes")
# traverseLushHills               = forBiomes("traverse:lush_hills")
# traverseLushSwamp               = forBiomes("traverse:green_swamp")
# traverseMeadow                  = forBiomes("traverse:meadow")
# traverseMiniJungle              = forBiomes("traverse:mini_jungle")
# traverseMountainousDesert       = forBiomes("traverse:mountainous_desert")
# traverseRedDesert               = forBiomes("traverse:red_desert")
# traverseRockyPlains             = forBiomes("traverse:rocky_plains")
# traverseRockyPlateau            = forBiomes("traverse:rocky_plateau")
# traverseSnowyConiferousForest   = forBiomes("traverse:snowy_coniferous_forest")
# traverseTemperateRainforest     = forBiomes("traverse:temperate_rainforest")
# traverseThicket                 = forBiomes("traverse:thicket")
# traverseWoodlands               = forBiomes("traverse:woodlands")

###########################################################################
# Nether biomes
# This variable should contain all Nether biomes.
###########################################################################

netherBiomes                    = forBiomes(vanillaHell, bopCorruptedSands, bopFungiForest, bopPhantasmagoricInferno, bopUndergarden, bopVisceralHeap)

###########################################################################
# End biomes
# This variable should contain all End biomes.
###########################################################################

endBiomes                       = forBiomes(vanillaTheEnd)

###########################################################################
# Void biomes
# This variable should contain all Void biomes.
###########################################################################

voidBiomes                      = forBiomes(vanillaTheVoid)

###########################################################################
# Overworld biomes
# This variable should contain all Overworld biomes.
###########################################################################

overworldBiomes                 = forAllBiomesExcept(netherBiomes, endBiomes, voidBiomes)
overworldBiomes.removeGeneratorBOP("glowshrooms")
overworldBiomes.removeGeneratorBOP("poison_ivy")

overworldBiomes.removeGeneratorBOP("emerald")
overworldBiomes.removeGeneratorBOP("ruby")
overworldBiomes.removeGeneratorBOP("peridot")
overworldBiomes.removeGeneratorBOP("topaz")
overworldBiomes.removeGeneratorBOP("tanzanite")
overworldBiomes.removeGeneratorBOP("malachite")
overworldBiomes.removeGeneratorBOP("sapphire")
overworldBiomes.removeGeneratorBOP("amber")

###########################################################################
# Ocean biomes
# This variable should contain all oceanic biomes.
###########################################################################

oceanBiomes                     = forBiomes(vanillaOcean, vanillaDeepOcean, bopCoralReef, bopKelpForest)
oceanBiomes.addSpawn("tropicraft:turtle", "WATER_CREATURE", 1, 4, 4)

###########################################################################
# Land biomes
# This variable should contain all land biomes.
###########################################################################

landBiomes                      = forAllBiomesExcept(oceanBiomes, netherBiomes, endBiomes, voidBiomes)

###########################################################################
# Icy overworld biomes
# This variable should contain all ICY overworld biomes.
###########################################################################

vanillaIcyOverworldBiomes       = forBiomes(vanillaIcePlains, vanillaIceMountains, vanillaColdBeach, vanillaColdTaiga, vanillaColdTaigaHills, vanillaIcePlainsSpikes, vanillaColdTaigaM, vanillaFrozenOcean)
bopIcyOverworldBiomes           = forBiomes(bopAlps, bopAlpsFoothills, bopColdDesert, bopSnowyConiferousForest, bopSnowyForest, bopGlacier)
# traverseIcyOverworldBiomes    = forBiomes(traverseGlacierSpikes, traverseSnowyConiferousForest)

###########################################################################
# Cool overworld biomes
# This variable should contain all COOL overworld biomes.
###########################################################################

vanillaCoolOverworldBiomes      = forBiomes(vanillaOcean, vanillaExtremeHillsEdge, vanillaExtremeHills, vanillaTaiga, vanillaTaigaHills, vanillaDeepOcean, vanillaStoneBeach, vanillaMegaTaiga, vanillaMegaTaigaHills, vanillaExtremeHillsPlus, vanillaExtremeHillsM, vanillaTaigaM, vanillaMegaSpruceTaiga, vanillaRedwoodTaigaHills, vanillaExtremeHillsPlusM)
bopCoolOverworldBiomes          = forBiomes(bopGravelBeach, bopMapleWoods, bopMountain, bopTundra, bopMeadow, bopMountainFoothills)
# traverseCoolOverworldBiomes   = forBiomes(traverseAutumnalWoodedHills, traverseBirchForestedHills, traverseCliffs, traverseForestedHills, traverseTemperateRainforest)

###########################################################################
# Warm overworld biomes
# This variable should contain all WARM overworld biomes.
###########################################################################

vanillaWarmOverworldBiomes      = forBiomes(vanillaPlains, vanillaForest, vanillaSwampland, vanillaMushroomIsland, vanillaMushroomIslandShore, vanillaBeach, vanillaForestHills, vanillaJungle, vanillaJungleHills, vanillaJungleEdge, vanillaBirchForest, vanillaBirchForestHills, vanillaRoofedForest, vanillaSunflowerPlains, vanillaFlowerForest, vanillaSwamplandM, vanillaJungleM, vanillaJungleEdgeM, vanillaBirchForestM, vanillaBirchForestHillsM, vanillaRoofedForestM)
bopWarmOverworldBiomes          = forBiomes(bopBayou, bopBog, bopBorealForest, bopChaparral, bopCherryBlossomGrove, bopConiferousForest, bopCrag, bopDeadForest, bopDeadSwamp, bopEucalyptusForest, bopFen, bopFlowerField, bopGrassland, bopGrove, bopHighland, bopLandOfLakes, bopLavenderFields, bopLushSwamp, bopMarsh, bopMoor, bopMysticGrove, bopOminousWoods, bopOrchard, bopOvergrownCliffs, bopPasture, bopPrairie, bopQuagmire, bopRainforest, bopRedwoodForest, bopSacredSprings, bopSeasonalForest, bopShield, bopShrubland, bopSteppe, bopTemperateRainforest, bopWasteland, bopWetland, bopWoodland, bopXericShrubland, bopCoralReef, bopKelpForest, bopMangrove, bopOriginBeach, bopOriginIsland, bopWhiteBeach, bopVolcanicIsland, bopFlowerIsland)
# traverseWarmOverworldBiomes   = forBiomes(traverseAutumnalWoods, traverseBadlands, traverseLushHills, traverseLushSwamp, traverseMeadow, traverseRockyPlains, traverseRockyPlateau, traverseThicket, traverseWoodlands)

###########################################################################
# Desert overworld biomes
# This variable should contain all DESERT overworld biomes.
###########################################################################

vanillaDesertOverworldBiomes    = forBiomes(vanillaDesert, vanillaDesertHills, vanillaSavanna, vanillaSavannaPlateau, vanillaMesa, vanillaMesaPlateauF, vanillaMesaPlateau, vanillaDesertM, vanillaSavannaM, vanillaSavannaPlateauM, vanillaMesaBryce, vanillaMesaPlateauFM, vanillaMesaPlateauM)
bopDesertOverworldBiomes        = forBiomes(bopBambooForest, bopBrushland, bopLushDesert, bopOutback, bopTropicalRainforest, bopOasis, bopTropicalIsland)
# traverseDesertOverworldBiomes = forBiomes(traverseAridHighland, traverseBadlands, traverseDesertShrubland, traverseMiniJungle, traverseMountainousDesert, traverseRedDesert)

###########################################################################
# Add all Overworld biomes to the biome pool with a default weight of 1.
# This needs to appear BEFORE biome weights are set.
###########################################################################

vanillaIcyOverworldBiomes.addToGeneration("ICY", 1)
vanillaCoolOverworldBiomes.addToGeneration("COOL", 1)
vanillaWarmOverworldBiomes.addToGeneration("WARM", 1)
vanillaDesertOverworldBiomes.addToGeneration("DESERT", 1)

bopIcyOverworldBiomes.addToGeneration("ICY", 1)
bopCoolOverworldBiomes.addToGeneration("COOL", 1)
bopWarmOverworldBiomes.addToGeneration("WARM", 1)
bopDesertOverworldBiomes.addToGeneration("DESERT", 1)

# traverseIcyOverworldBiomes.addToGeneration("ICY", 1)
# traverseCoolOverworldBiomes.addToGeneration("COOL", 1)
# traverseWarmOverworldBiomes.addToGeneration("WARM", 1)
# traverseDesertOverworldBiomes.addToGeneration("DESERT", 1)

###########################################################################
# Biome weights
# Controls how frequently biomes are generated.
# You may change the weights to whatever you want.
# Weights must be non-negative integers.
###########################################################################

# Icy biomes
###########################################################################
# Weight = 48
###########################################################################
vanillaIcePlains.set("genWeight", 9)
vanillaIceMountains.set("genWeight", 4)
vanillaColdBeach.set("genWeight", 2)
vanillaColdTaiga.set("genWeight", 4)
vanillaColdTaigaHills.set("genWeight", 3)
vanillaIcePlainsSpikes.set("genWeight", 2)
vanillaColdTaigaM.set("genWeight", 1)
vanillaFrozenOcean.set("genWeight", 2)

bopAlps.set("genWeight", 5)
bopAlpsFoothills.set("genWeight", 4)
bopColdDesert.set("genWeight", 0)
bopSnowyConiferousForest.set("genWeight", 5)
bopSnowyForest.set("genWeight", 4)
bopGlacier.set("genWeight", 3)


# Cold biomes
###########################################################################
# Weight = 39
###########################################################################
vanillaOcean.set("genWeight", 2)
vanillaExtremeHills.set("genWeight", 5)
vanillaTaiga.set("genWeight", 2)
vanillaTaigaHills.set("genWeight", 3)
vanillaDeepOcean.set("genWeight", 2)
vanillaStoneBeach.set("genWeight", 3)
vanillaMegaTaiga.set("genWeight", 1)
vanillaTaigaHills.set("genWeight", 3)
vanillaExtremeHillsPlus.set("genWeight", 4)
vanillaExtremeHillsM.set("genWeight", 3)
vanillaTaigaM.set("genWeight", 1)
vanillaMegaSpruceTaiga.set("genWeight", 2)
vanillaRedwoodTaigaHills.set("genWeight", 1)
vanillaExtremeHillsPlusM.set("genWeight", 1)
vanillaExtremeHillsEdge.set("genWeight", 2)

bopGravelBeach.set("genWeight", 0)
bopMapleWoods.set("genWeight", 0)
bopMountain.set("genWeight", 3)
bopTundra.set("genWeight", 0)
bopMountainFoothills.set("genWeight", 1)

# Warm biomes
###########################################################################
# Weight = 145
###########################################################################
vanillaPlains.set("genWeight", 12)
vanillaForest.set("genWeight", 5)
vanillaForestHills.set("genWeight", 6)
vanillaSwampland.set("genWeight", 1)
vanillaSwamplandM.set("genWeight", 1)
vanillaMushroomIsland.set("genWeight", 0)
vanillaMushroomIslandShore.set("genWeight", 0)
vanillaBeach.set("genWeight", 3)
vanillaJungle.set("genWeight", 3)
vanillaJungleEdge.set("genWeight", 3)
vanillaJungleEdgeM.set("genWeight", 3)
vanillaJungleHills.set("genWeight", 4)
vanillaJungleM.set("genWeight", 2)
vanillaBirchForest.set("genWeight", 3)
vanillaBirchForestHills.set("genWeight", 3)
vanillaBirchForestHillsM.set("genWeight", 2)
vanillaBirchForestM.set("genWeight", 2)
vanillaRoofedForest.set("genWeight", 2)
vanillaSunflowerPlains.set("genWeight", 4)
vanillaFlowerForest.set("genWeight", 2)
vanillaRoofedForestM.set("genWeight", 1)

bopBayou.set("genWeight", 3)
bopBog.set("genWeight", 0)
bopBorealForest.set("genWeight", 0)
bopChaparral.set("genWeight", 3)
bopCherryBlossomGrove.set("genWeight", 3)
bopConiferousForest.set("genWeight", 5)
bopCrag.set("genWeight", 2)
bopDeadForest.set("genWeight", 0)
bopDeadSwamp.set("genWeight", 1)
bopEucalyptusForest.set("genWeight", 1)
bopFen.set("genWeight", 1)
bopFlowerField.set("genWeight", 1)
bopGrassland.set("genWeight", 0)
bopGrove.set("genWeight", 1)
bopHighland.set("genWeight", 4)
bopLandOfLakes.set("genWeight", 1)
bopLavenderFields.set("genWeight", 2)
bopLushSwamp.set("genWeight", 4)
bopMarsh.set("genWeight", 2)
bopMeadow.set("genWeight", 1)
bopMoor.set("genWeight", 0)
bopMysticGrove.set("genWeight", 0)
bopOminousWoods.set("genWeight", 0)
bopOrchard.set("genWeight", 3)
bopOvergrownCliffs.set("genWeight", 5)
bopPasture.set("genWeight", 5)
bopPrairie.set("genWeight", 5)
bopQuagmire.set("genWeight", 0)
bopRainforest.set("genWeight", 1)
bopRedwoodForest.set("genWeight", 5)
bopSacredSprings.set("genWeight", 0)
bopSeasonalForest.set("genWeight", 0)
bopShield.set("genWeight", 1)
bopShrubland.set("genWeight", 0)
bopSteppe.set("genWeight", 0)
bopTemperateRainforest.set("genWeight", 2)
bopWasteland.set("genWeight", 1)
bopWetland.set("genWeight", 0)
bopWoodland.set("genWeight", 1)
bopXericShrubland.set("genWeight", 3)
bopCoralReef.set("genWeight", 5)
bopKelpForest.set("genWeight", 5)
bopMangrove.set("genWeight", 2)
bopOriginBeach.set("genWeight", 0)
bopOriginIsland.set("genWeight", 0)
bopWhiteBeach.set("genWeight", 0)
bopVolcanicIsland.set("genWeight", 4)
bopFlowerIsland.set("genWeight", 0)

# Desert biomes
###########################################################################
# Weight = 71
###########################################################################
vanillaDesert.set("genWeight", 10)
vanillaDesertHills.set("genWeight", 2)
vanillaDesertM.set("genWeight", 1)
vanillaSavanna.set("genWeight", 7)
vanillaSavannaM.set("genWeight", 4)
vanillaSavannaPlateau.set("genWeight", 6)
vanillaSavannaPlateauM.set("genWeight", 3)
vanillaMesa.set("genWeight", 4)
vanillaMesaBryce.set("genWeight", 3)
vanillaMesaPlateau.set("genWeight", 2)
vanillaMesaPlateauF.set("genWeight", 0)
vanillaMesaPlateauFM.set("genWeight", 1)
vanillaMesaPlateauM.set("genWeight", 1)

bopBambooForest.set("genWeight", 5)
bopBrushland.set("genWeight", 5)
bopLushDesert.set("genWeight", 4)
bopOutback.set("genWeight", 3)
bopTropicalRainforest.set("genWeight", 2)
bopOasis.set("genWeight", 6)
bopTropicalIsland.set("genWeight", 2)


###########################################################################
# Traverse Biomes ## Uncomment when adding Traverse.
###########################################################################
# traverseAridHighland.set("genWeight", 2)
# traverseAutumnalWoodedHills.set("genWeight", 1)
# traverseAutumnalWoods.set("genWeight", 2)
# traverseBadlands.set("genWeight", 2)
# traverseBirchForestedHills.set("genWeight", 2)
# traverseCliffs.set("genWeight", 0)
# traverseDesertShrubland.set("genWeight", 0)
# traverseForestedHills.set("genWeight", 2)
# traverseGlacierSpikes.set("genWeight", 1)
# traverseLushHills.set("genWeight", 2)
# traverseLushSwamp.set("genWeight", 1)
# traverseMeadow.set("genWeight", 0)
# traverseMiniJungle.set("genWeight", 1)
# traverseMountainousDesert.set("genWeight", 0)
# traverseRedDesert.set("genWeight", 2)
# traverseRockyPlains.set("genWeight", 2)
# traverseRockyPlateau.set("genWeight", 0)
# traverseSnowyConiferousForest.set("genWeight", 0)
# traverseTemperateRainforest.set("genWeight", 0)
# traverseThicket.set("genWeight", 2)
# traverseWoodlands.set("genWeight", 0)

###########################################################################
# Set the biome size for the "Realistic" world type
# Default is 4; Large Biomes is 6
###########################################################################

Tweaker.setAverageBiomeSize("RTG", 4)

###########################################################################
# Set the biomes that players can spawn in
# By default, players may spawn in any land biome.
# You may use any of the variables above to set spawn biomes.
# For example:
# vanillaFlowerForest.set("isSpawnBiome", true)
# overworldBiomes.set("isSpawnBiome", true)
# vanillaIcyOverworldBiomes.set("isSpawnBiome", false)
###########################################################################

oceanBiomes.set("isSpawnBiome", false)

###########################################################################
# This biomes will be not added,
# Every river biome should not be enabled!
###########################################################################

bopGravelBeach.registerGenBiomeRep(25)

###########################################################################
# Removing Wasteland DicType from all others than BOPWasteland biome
###########################################################################
notWasteland = forBiomes(bopVolcanicIsland, bopGlacier, bopTundra, vanillaIcePlains, bopQuagmire, bopCrag)
notWasteland.removeDicTypes("WASTELAND")